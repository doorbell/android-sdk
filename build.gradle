buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.doorbell'
version = '0.2.6'

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSonatypeUsername() {
    return hasProperty('sonatypeUsername') ? sonatypeUsername : ""
}

def getSonatypePassword() {
    return hasProperty('sonatypePassword') ? sonatypePassword : ""
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: getReleaseRepositoryUrl()) {
            authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
        }

        pom.project {
            name 'Doorbell SDK'
            packaging 'aar'
            description 'In-app user feedback gathering SDK for Doorbell.io'
            url 'https://github.com/doorbell/android-sdk'

            scm {
                url 'scm:git@github.com:doorbell/android-sdk.git'
                connection 'scm:git@github.com:doorbell/android-sdk.git'
                developerConnection 'scm:git@github.com:doorbell/android-sdk.git'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'manavo'
                    name 'Philip Manavopoulos'
                }
            }
        }
    }
}

android {
    compileSdkVersion 22 // TODO remove Apache HTTP dependencies
    buildToolsVersion '23.0.2'
}
