apply plugin: "maven"
apply plugin: "signing"

signing {
    required { hasProperty("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def getReleaseRepositoryUrl() {
    return project.hasProperty("RELEASE_REPOSITORY_URL") ? project.property("RELEASE_REPOSITORY_URL")
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSonatypeUsername() {
    return project.hasProperty("sonatypeUsername") ? project.property("sonatypeUsername") : ""
}

def getSonatypePassword() {
    return project.hasProperty("sonatypePassword") ? project.property("sonatypePassword") : ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.archives

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
            }

            pom {
                project {
                    name "Doorbell SDK"
                    packaging "aar"
                    description "In-app user feedback gathering SDK for Doorbell.io"
                    url "https://github.com/doorbell/android-sdk"

                    scm {
                        url "scm:git@github.com:doorbell/android-sdk.git"
                        connection "scm:git@github.com:doorbell/android-sdk.git"
                        developerConnection "scm:git@github.com:doorbell/android-sdk.git"
                    }

                    licenses {
                        license {
                            name "MIT License"
                            url "http://www.opensource.org/licenses/mit-license.php"
                            distribution "repo"
                        }
                    }

                    developers {
                        developer {
                            id "manavo"
                            name "Philip Manavopoulos"
                        }
                    }
                }
            }
        }
    }
}

/**
 * Generate Javadocs.
 */
task androidJavadocs(type: Javadoc) {
    description "Generate Javadocs."

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

/**
 * Generate Jar of Javadocs.
 */
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    description "Generate Javadoc Jar."
    classifier = "javadoc"

    from androidJavadocs.destinationDir
}

/**
 * Generate Java Sources Jar.
 */
task androidSourcesJar(type: Jar) {
    description "Generates Java Sources."
    classifier = "sources"

    from android.sourceSets.main.java.sourceFiles
}

// "android-sdk-release.aar" is automatically added to "artifacts"
artifacts {
    archives androidSourcesJar
//    archives androidJavadocsJar
}
